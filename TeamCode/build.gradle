//
// build.gradle in TeamCode
//
// Most of the definitions for building your module reside in a common, shared
// file 'build.common.gradle'. Being factored in this way makes it easier to
// integrate updates to the FTC into your code. If you really need to customize
// the build definitions, you can place those customizations in this file, but
// please think carefully as to whether such customizations are really necessary
// before doing so.


buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://repo.dairy.foundation/releases"
        }
    }
    dependencies {
        classpath "dev.frozenmilk:Load:0.2.4"
    }
}

// Custom definitions may go here

// Include common definitions from above.
apply from: '../build.common.gradle'
apply from: '../build.dependencies.gradle'
apply plugin: 'dev.frozenmilk.sinister.sloth.load'

tasks.withType(dev.frozenmilk.sinister.sloth.DeploySloth).configureEach {
    adbExecutable = rootProject.file("tools/adb-serial.bat")
}

tasks.withType(dev.frozenmilk.sinister.sloth.RemoveRemoteSloth).configureEach {
    adbExecutable = rootProject.file("tools/adb-serial.bat")
}

def robotConfigXmlFiles = [
        "oraclegit.xml",
        "oraclev3git.xml"
]

tasks.register("pushRobotConfigXml") {
    group = "install"
    description = "Push selected TeamCode XML config files to /sdcard/FIRST on the connected RC/Control Hub."

    doLast {
        def adbPath = rootProject.file("tools/adb-serial.bat").absolutePath
        def xmlDir = project.file("src/main/res/xml")

        robotConfigXmlFiles.each { fileName ->
            def sourceFile = new File(xmlDir, fileName)
            if (!sourceFile.exists()) {
                logger.warn("Skipping missing XML config: ${sourceFile}")
                return
            }
            exec {
                commandLine adbPath, "push", sourceFile.absolutePath, "/sdcard/FIRST/"
            }
        }
    }
}

tasks.named("deploySloth") {
    finalizedBy("pushRobotConfigXml")
}

android {
    namespace = 'org.firstinspires.ftc.teamcode'

    packagingOptions {
        jniLibs.useLegacyPackaging true
    }
}

repositories {
    maven {
        url = "https://repo.dairy.foundation/releases"
    }
}

dependencies {
    implementation project(':FtcRobotController')
    implementation 'org.ftclib.ftclib:core:2.1.1' // core
    implementation 'org.psilynx.psikit:core:0.1.0-beta2'
    implementation 'org.psilynx.psikit:ftc:0.1.0-beta2'
    implementation("dev.frozenmilk.sinister:Sloth:0.2.4")

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.12.1'
}

afterEvaluate {
    def unitTestTask = tasks.findByName('testDebugUnitTest')
    if (unitTestTask != null) {
        tasks.register('runRlogSummary', JavaExec) {
            group = 'verification'
            description = 'Summarize an RLOG file into CSV stats (PC CLI).'

            dependsOn(unitTestTask.dependsOn)
            classpath = unitTestTask.classpath
            mainClass = 'org.firstinspires.ftc.teamcode.psikit.RlogSummaryCliMain'

            def pLog = project.findProperty('psikitReplayLog')
            def pPrefixes = project.findProperty('summaryPrefixes')
            def pKeyContains = project.findProperty('summaryKeyContains')

            if (pLog != null) {
                systemProperty 'psikitReplayLog', pLog.toString()
            }
            if (pPrefixes != null) {
                systemProperty 'summaryPrefixes', pPrefixes.toString()
            }
            if (pKeyContains != null) {
                systemProperty 'summaryKeyContains', pKeyContains.toString()
            }
        }
    }
}
